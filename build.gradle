plugins {
	id 'java'
	id 'org.springframework.boot' version '3.1.4'
	id 'io.spring.dependency-management' version '1.1.3'
  id 'com.adarshr.test-logger' version '3.2.0'
}

group = 'com.example'
version = '0.0.1-SNAPSHOT'

java {
	sourceCompatibility = '17'
}

repositories {
  mavenCentral()
}

sourceSets {
  integrationTest {
    compileClasspath += main.output
    runtimeClasspath += main.output
    resources.srcDir "$projectDir/src/integration/resources"
  }
}

configurations {
  integrationTestImplementation.extendsFrom testImplementation
  integrationTestRuntime.extendsFrom testRuntime
}

tasks.register('integrationTest', Test) {
  useJUnitPlatform()

  classpath = sourceSets.integrationTest.runtimeClasspath
  testClassesDirs = sourceSets.integrationTest.output.classesDirs

  testLogging {
    events "standardOut" 
  }

  outputs.upToDateWhen {
    false
  }
}

check.dependsOn integrationTest

test {
  testLogging {
    events "passed", "skipped", "failed", "standardOut" 
  }
  outputs.upToDateWhen {
    false
  }
}

dependencies {
  implementation 'com.google.code.gson:gson:2.10.1'
  implementation 'org.springframework.kafka:spring-kafka:3.0.12'
  implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
  implementation group: 'org.postgresql', name: 'postgresql', version: '42.6.0'
  implementation 'org.springframework.boot:spring-boot-starter-web'
  implementation "org.springframework.boot:spring-boot-starter-log4j2"
  modules {
      module("org.springframework.boot:spring-boot-starter-logging") {
          replacedBy("org.springframework.boot:spring-boot-starter-log4j2", "Use Log4j2 instead of Logback")
      }
  }
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
  testImplementation 'io.rest-assured:rest-assured:5.3.2'
  testImplementation 'org.mockito:mockito-core:3.+'
  integrationTestImplementation 'org.springframework.boot:spring-boot-starter-test'
  integrationTestImplementation 'io.rest-assured:rest-assured:5.3.2'
}

tasks.named('test') {
	useJUnitPlatform()
}
